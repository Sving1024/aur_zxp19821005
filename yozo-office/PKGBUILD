# Maintainer: WhriedPlanck
# Contributor: Felix Golatofski <contact@xdfr.de>
# Contributor: h0cheung <unknow>
# Contributor: WhiredPlanck <unknow>
# Contributor: heavysink <unknow>
pkgbase=yozo-office
pkgname=(
    "${pkgbase}"
    "${pkgbase}-fonts"
    "${pkgbase}-templates"
)
pkgver=9.0.3988.101ZH.S1
_relver=20231211_091218_231231
pkgrel=1
pkgdesc="Yozo Office 2019 - An M$ Office compatible office suite"
url="https://www.yozosoft.com/product-officelinux.html"
options=(
    '!strip'
)
license=('LicenseRef-custom')
arch=('x86_64')
source=("${pkgbase}-${pkgver}.deb::https://dl.yozosoft.com/yozo/project/file/${_relver}/${pkgbase}_${pkgver}_amd64.deb")
sha256sums=('84613f2e3243ebbd94445db0097df97ddb445ea3d2b31ad958bd446f06c52050')
_info() { echo -e "[\e[96m$*\e[0m]"; }
prepare() {
    cd "${srcdir}"
    # Extracting data.tar.xz ...
    bsdtar -xf "${srcdir}/data."*
    # Fixing directory permissions ...
    find "${srcdir}" -type d -exec chmod -v 755 {} +
    # Removing unnecessary files ...
    rm -rv "${srcdir}/etc/xdg"
    rm -rv "${srcdir}/opt/Yozosoft/Yozo_Office/"{Upgrade,uninstall}
    rm -rv "${srcdir}"/usr/share/applications/yozo-uninstall.desktop
}
package_yozo-office() {
    depends=(
        'gtk2'
        'libxml2'
        'freetype2'
    )
    optdepends=(
        "${pkgbase}-fonts: built-in fonts"
        "${pkgbase}-templates: built-in document templates"
    )   
    # Installing main binaries ...
    cp -rv "${srcdir}"/{usr,opt,etc} "${pkgdir}"
    rm -rv "${pkgdir}"/usr/share/fonts
    rm -rv "${pkgdir}"/opt/Yozosoft/Yozo_Office/Templates
    # Fixing permissions of binaries in /usr/bin ...
    find "${pkgdir}"/usr/bin -type f -exec chmod -v 755 {} +
    # Replacing libfreetype.so.6 with system's to adapt for harfbuzz 5.x
    rm -rv "${pkgdir}/opt/Yozosoft/Yozo_Office/Jre/lib/amd64/libfreetype.so.6"
    ln -sv "/usr/lib/libfreetype.so.6" "${pkgdir}/opt/Yozosoft/Yozo_Office/Jre/lib/amd64/libfreetype.so.6"
    # Installing licenses of thirdparties ...
    install -Dvm644 "${pkgdir}"/opt/Yozosoft/Yozo_Office/thirdpartylicensereadme.txt \
        "${pkgdir}"/usr/share/licenses/${pkgname}/thirdpartylicensereadme.txt
}
package_yozo-office-fonts() {
    pkgdesc="Built-in fonts of Yozo Office 2019"
    cd "${srcdir}"
    # Installing built-in fonts of Yozo Office 2019 ...
    install -dvm644 "${pkgdir}"/usr/share/fonts/truetype
    cp -rv "${srcdir}"/usr/share/fonts/truetype/yozo \
        "${pkgdir}"/usr/share/fonts/truetype
}
package_yozo-office-templates() {
    pkgdesc="Built-in document templates of Yozo Office 2019"
    cd "${srcdir}"
    # Installing built-in document templates of Yozo Office 2019 ...
    install -dvm755 "${pkgdir}"/opt/Yozosoft/Yozo_Office
    cp -rv "${srcdir}"/opt/Yozosoft/Yozo_Office/Templates \
        "${pkgdir}"/opt/Yozosoft/Yozo_Office
}