# Maintainer: Azure Zeng <weedycn at outlook dot com>
# Contributor: Pylogmon <pylogmon@outlook.com>
_appname=clash-verge
pkgname="${_appname}-rev-bin"
_pkgname='Clash Verge'
pkgver=2.3.0
pkgrel=99
pkgdesc="Continuation of Clash Verge | A Clash Meta GUI based on Tauri.(Prebuilt version)"
arch=(
    'aarch64'
    'armv7h'
    'x86_64'
)
url="https://www.clashverge.dev/"
_ghurl="https://github.com/clash-verge-rev/clash-verge-rev"
license=('GPL-3.0-only')
conflicts=(
    "${pkgname%-bin}"
    "${pkgname%-alpha}"
    "${_appname}"
)
provides=("${pkgname%-bin}=${pkgver}")
depends=(
    'webkit2gtk-4.1'
    'gtk3'
    'libayatana-appindicator'
)
options=('!strip')
source_aarch64=("${pkgname%-bin}-${pkgver}-aarch64.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.aarch64.rpm")
source_armv7h=("${pkgname%-bin}-${pkgver}-armv7h.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.armhfp.rpm")
source_x86_64=("${pkgname%-bin}-${pkgver}-x86_64.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.x86_64.rpm")
sha512sums_aarch64=('547a43959ce08b9b6f51b98e5b7e5f8b740fbdb04598081a8b4cb5dfdd939b3f6ba4b1a99f5bae1e945e17e9cf66c300dfb82656659d76abed85ad6a5bff3adb')
sha512sums_armv7h=('12d96446940846d0760d3ed3c4ac835887c7822c7c28b7fae06e066afa08313db01b801845f43a955db46230693bfb848a18b6f8427b9f63004216647b99132f')
sha512sums_x86_64=('355594481349247c6731fb7fc6cdf9f3ab6eab9c4ddf263d94279cf2cd195d79558e8c1b02d7e544caf18aa2757173e41eb242202099c40136cfb4b555895b0b')
prepare() {
    sed -i -e "
        s/Development/Network/g
        s/Icon=${_appname}/Icon=${pkgname%-bin}/g
    " "${srcdir}/usr/share/applications/${_pkgname}.desktop"
}
package() {
    install -Dm755 "${srcdir}/usr/bin/"* -t "${pkgdir}/usr/bin"
    cp -Pr --no-preserve=ownership "${srcdir}/usr/lib" "${pkgdir}/usr"
    install -Dm644 "${srcdir}/usr/share/applications/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname%-bin}.desktop"
    for _icons in "${_icon_sizes[@]}";do
        install -Dm644 "${srcdir}/usr/share/icons/hicolor/${_icons}/apps/${_appname}.png" \
            "${pkgdir}/usr/share/icons/hicolor/${_icons//@2/}/apps/${pkgname%-bin}.png"
    done
}