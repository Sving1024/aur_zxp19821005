# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
_appname=clash-for-windows
pkgname="${_appname}-zh-bin"
_pkgname=Clash.for.Windows
_shortname=cfw
_pkgver=0.20.39
_subver=3
pkgver="${_pkgver}.${_subver}"
_electronversion=34
pkgrel=1
pkgdesc="A Windows/macOS/Linux GUI based on Clash and Electron.Chinese version.(Prebuilt version.Use system-wide electron).clash for windows汉化版."
arch=(
    'aarch64'
    'x86_64'
)
url='https://github.com/Z-Siqi/Clash-for-Windows_Chinese'
provides=("${_appname}=${pkgver}")
conflicts=(
    "${pkgname%-bin}"
    "${_appname}"
)
depends=(
    "electron${_electronversion}"
)
makedepends=(
    'gendesk'
)
optdepends=(
    'nftables: TUN mode required.'
    'iproute2: TUN mode required.'
)
options=(
    '!strip'
)
source=(
    "${pkgname%-bin}-${pkgver}.png::https://raw.githubusercontent.com/Z-Siqi/Clash-for-Windows_Chinese/${pkgver}/app/main/dist/electron/static/imgs/icon_512.png"
    "LICENSE-${pkgver}.txt::https://raw.githubusercontent.com/Z-Siqi/Clash-for-Windows_Chinese/${pkgver}/app/main/ABOUT/LICENSE.electron.txt"
)
source_aarch64=("${pkgname}-${pkgver}-aarch64-linux.tar.gz::${url}/releases/download/${pkgver}/${_pkgname}-${_pkgver}-Opt.${_subver}-linux-arm64.tar.gz")
source_x86_64=("${pkgname}-${pkgver}-x86_64-linux.tar.gz::${url}/releases/download/${pkgver}/${_pkgname}-${_pkgver}-Opt.${_subver}-linux-x64.tar.gz")
sha256sums=('6ac969baa665cbaafb52b67d5d9236e42a16bebfdcc204d9225dc210035e1232'
            '5154e165bd6c2cc0cfbcd8916498c7abab0497923bafcd5cb07673fe8480087d')
sha256sums_aarch64=('e4d86431ac0a6dd9ff7a5b0cd087770b241d7bc1b1ac62ca72b03cf05f2a386f')
sha256sums_x86_64=('e383c7147422367b64ed22e266bd5bbd3d1a2f1ad750d5a36cc2173de4751f58')
_get_electron_version() {
    _electronversion="$(strings "${srcdir}/${_shortname}-linux-"*/${_shortname} | grep '^Chrome/[0-9.]* Electron/[0-9]' | cut -d'/' -f3 | cut -d'.' -f1)"
    echo -e "The electron version is: \033[1;31m${_electronversion}\033[0m"
}
prepare() {
    gendesk -q -f -n \
        --pkgname="${pkgname%-bin}" \
        --pkgdesc="${pkgdesc}" \
        --categories="Network" \
        --name="${_pkgname}" \
        --exec="${pkgname%-bin} --no-sandbox %U"
    _get_electron_version
    _file_list=(chrome_100_percent.pak chrome_200_percent.pak chrome-sandbox icudtl.dat libEGL.so libffmpeg.so \
        libGLESv2.so libvk_swiftshader.so libvulkan.so.1 resources.pak vk_swiftshader_icd.json)
    for _files in "${_file_list[@]}";do
        ln -sf "/usr/lib/electron${_electronversion}/${_files}" "${srcdir}/${_shortname}-linux-"*/"${_files}"
    done
}
package() {
    install -Dm755 -d "${pkgdir}/usr/"{bin,lib/"${pkgname%-bin}"}
    cp -Pr --no-preserve=ownership "${srcdir}/${_shortname}-linux-"*/*  "${pkgdir}/usr/lib/${pkgname%-bin}"
    ln -sf "/usr/lib/${pkgname%-bin}/${_shortname}" "${pkgdir}/usr/bin/${pkgname%-bin}"
    install -Dm644 "${srcdir}/LICENSE-${pkgver}.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
    install -Dm644 "${srcdir}/${pkgname%-bin}-${pkgver}.png" "${pkgdir}/usr/share/pixmaps/${pkgname%-bin}.png"
    install -Dm644 "${srcdir}/${pkgname%-bin}.desktop" -t "${pkgdir}/usr/share/applications"
}