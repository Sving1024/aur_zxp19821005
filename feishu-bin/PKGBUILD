# Generated by debtap
# Maintainer: Allen Zhong <pdev@zhoal.pw>
# Maintainer: Xuanwo <github@xuanwo.io>
# Maintainer: Zhou Zhiqiang <aur@strrl.dev>
# Contributor: Feishu Linux Team <contact@feishu.cn>
pkgname=feishu-bin
_pkgname="bytedance-${pkgname%-bin}"
_appname=Feishu
pkgver=7.18.11
#_pkgrel=24
_pkgtyp=stable
_pkghash_x64=88a232d5
_pkghash_arm64=f29ac73d
pkgrel=1
pkgdesc="Linux client of Feishu (Lark) from Bytedance."
arch=(
    'aarch64'
    'x86_64'
)
url="https://www.feishu.cn"
_dlurl="https://sf3-cn.feishucdn.com/obj/ee-appcenter"
license=('LicenseRef-custom')
depends=(
    'gtk3'
    'nss'
    'xdg-utils'
    'alsa-lib'
    'libpulse'
    'libmfx'
)
conflicts=("${pkgname%-bin}")
provides=(
    "${_pkgname}"
    "${pkgname%-bin}"
)
options=(
    '!strip'
    '!emptydirs'
)
install="${pkgname%-bin}.install"
source=("LICENSE.html::${url}/terms")
source_aarch64=("${pkgname%-bin}-${pkgver}-aarch64.deb::${_dlurl}/${_pkghash_arm64}/${_appname}-linux_arm64-${pkgver}.deb")
source_x86_64=("${pkgname%-bin}-${pkgver}-x86_64.deb::${_dlurl}/${_pkghash_x64}/${_appname}-linux_x64-${pkgver}.deb")
sha256sums=('fcac38a5d4dd6b8817907de32b323992a5adbe960388431404772a9ae4c60557')
sha256sums_aarch64=('8210163d08151014a89bbcc11d429be7a3b7119478ace9d0973f41452241369e')
sha256sums_x86_64=('127783556f1e43a27e338f619fdc4bb6f96a8833b1e11b3c54c2edd5ca926da9')
build() {
    bsdtar -xf "${srcdir}/data."*
    sed "s|${_pkgname}|${pkgname%-bin}|g" -i "${srcdir}/usr/share/appdata/${_pkgname}.appdata.xml"
    sed "s|/usr/bin/${_pkgname}-stable|${pkgname%-bin}|g;s|${_pkgname}|${pkgname%-bin}|g" -i "${srcdir}/usr/share/applications/${_pkgname}.desktop"
}
package(){
    cp -r "${srcdir}/opt" "${pkgdir}"
    install -Dm644 "${srcdir}/usr/share/appdata/${_pkgname}.appdata.xml" "${pkgdir}/usr/share/appdata/${pkgname%-bin}.appdata.xml"
    install -Dm644 "${srcdir}/usr/share/applications/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname%-bin}.desktop"
    install -Dm644 "${srcdir}/usr/share/doc/${_pkgname}-stable/changelog.gz" -t "${pkgdir}/usr/share/doc/${pkgname%-bin}"
    install -Dm644 "${srcdir}/usr/share/man/man1/${_pkgname}-stable.1.gz" "${srcdir}/usr/share/man/man1/${pkgname%-bin}-stable.1.gz"
    install -Dm644 "${srcdir}/usr/share/man/man1/${_pkgname}.1.gz" "${srcdir}/usr/share/man/man1/${pkgname%-bin}.1.gz"
    install -Dm644 "${srcdir}/usr/share/menu/${_pkgname}.menu" "${srcdir}/usr/share/menu/${pkgname%-bin}.menu"
    for _icons in 16 24 32 48 64 128 256;do
        install -Dm644 "${srcdir}/opt/bytedance/${pkgname%-bin}/product_logo_${_icons}.png" \
            "${pkgdir}/usr/share/icons/hicolor/${_icons}x${_icons}/apps/${pkgname%-bin}.png"
    done
    install -Dm644 "${srcdir}/opt/bytedance/${pkgname%-bin}/product_logo_32.xpm" "${pkgdir}/usr/share/pixmaps/${pkgname%-bin}.xpm"
    install -Dm644 "${srcdir}/LICENSE.html" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}