# Generated by debtap
# Maintainer: Allen Zhong <pdev@zhoal.pw>
# Maintainer: Xuanwo <github@xuanwo.io>
# Maintainer: Zhou Zhiqiang <aur@strrl.dev>
# Contributor: Feishu Linux Team <contact@feishu.cn>
pkgname=feishu-bin
_pkgname="bytedance-${pkgname%-bin}"
_appname=Feishu
pkgver=7.22.9
_pkgtyp=stable
_pkghash_x64=bfdb886c
_pkghash_arm64=c3f495d6
pkgrel=1
pkgdesc="Linux client of Feishu (Lark) from Bytedance."
arch=(
    'aarch64'
    'x86_64'
)
url="https://www.feishu.cn"
_dlurl="https://sf3-cn.feishucdn.com/obj/ee-appcenter"
license=('LicenseRef-custom')
depends=(
    'gtk3'
    'nss'
    'xdg-utils'
    'alsa-lib'
    'libpulse'
    'libmfx'
)
conflicts=("${pkgname%-bin}")
provides=(
    "${_pkgname}"
    "${pkgname%-bin}"
)
options=(
    '!strip'
    '!emptydirs'
)
install="${pkgname%-bin}.install"
source=("LICENSE.html::${url}/terms")
source_aarch64=("${pkgname%-bin}-${pkgver}-aarch64.deb::${_dlurl}/${_pkghash_arm64}/${_appname}-linux_arm64-${pkgver}.deb")
source_x86_64=("${pkgname%-bin}-${pkgver}-x86_64.deb::${_dlurl}/${_pkghash_x64}/${_appname}-linux_x64-${pkgver}.deb")
sha256sums=('ef8e48d7337192f16c22ea87583714c6523ee7c0ff0bdbd88c88bbca78cb5289')
sha256sums_aarch64=('713f67d69db6d326b54f6d5f5b2e1bedbedd231de1ab0ee50a89c6692676f940')
sha256sums_x86_64=('e252c241e5ba651ce6ceb1cf43dd51c4a12a242c6aa9ae221ae27cb2766abe44')
build() {
    bsdtar -xf "${srcdir}/data."*
    sed "s|${_pkgname}|${pkgname%-bin}|g" -i "${srcdir}/opt/bytedance/${pkgname%-bin}/${_pkgname}"
    sed "s|/usr/bin/${_pkgname}-${_pkgtyp}|${pkgname%-bin}|g;s|Icon=${_pkgname}|Icon=${pkgname%-bin}|g" -i "${srcdir}/usr/share/applications/${_pkgname}.desktop"
    sed "16i\StartupWMClass=${pkgname%-bin}" -i "${srcdir}/usr/share/applications/${_pkgname}.desktop"
    sed "s|${_pkgname}|${pkgname%-bin}|g" -i "${srcdir}/usr/share/appdata/${_pkgname}.appdata.xml"
    sed "s|${_pkgname}-${_pkgtyp}|${pkgname%-bin}|g;s|${_pkgname}|${pkgname%-bin}|g" -i "${srcdir}/usr/share/menu/${_pkgname}.menu"
}
package(){
    cp -r "${srcdir}/opt" "${pkgdir}"
    install -Dm755 -d "${pkgdir}/usr/bin"
    ln -sf "${srcdir}/opt/bytedance/${pkgname%-bin}/${_pkgname}" "${pkgdir}/usr/bin/${pkgname%-bin}"
    install -Dm644 "${srcdir}/usr/share/appdata/${_pkgname}.appdata.xml" "${pkgdir}/usr/share/appdata/${pkgname%-bin}.appdata.xml"
    install -Dm644 "${srcdir}/usr/share/applications/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname%-bin}.desktop"
    install -Dm644 "${srcdir}/usr/share/doc/${_pkgname}-${_pkgtyp}/changelog.gz" -t "${pkgdir}/usr/share/doc/${pkgname%-bin}"
    install -Dm644 "${srcdir}/usr/share/man/man1/${_pkgname}-${_pkgtyp}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname%-bin}-${_pkgtyp}.1.gz"
    install -Dm644 "${srcdir}/usr/share/man/man1/${_pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname%-bin}.1.gz"
    install -Dm644 "${srcdir}/usr/share/menu/${_pkgname}.menu" "${pkgdir}/usr/share/menu/${pkgname%-bin}.menu"
    for _icons in 16 24 32 48 64 128 256;do
        install -Dm644 "${srcdir}/opt/bytedance/${pkgname%-bin}/product_logo_${_icons}.png" \
            "${pkgdir}/usr/share/icons/hicolor/${_icons}x${_icons}/apps/${pkgname%-bin}.png"
    done
    install -Dm644 "${srcdir}/opt/bytedance/${pkgname%-bin}/product_logo_32.xpm" "${pkgdir}/usr/share/pixmaps/${pkgname%-bin}.xpm"
    install -Dm644 "${srcdir}/LICENSE.html" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}