# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
pkgname=vookiimageviewer-bin
_pkgname=VookiImageViewer
pkgver=2024.05.12
_exiv2version=0.27.1
pkgrel=2
pkgdesc="A cross-platform lightweight image viewer for a fast image preview."
arch=('x86_64')
url="https://vookiimageviewer.cz/"
_ghurl="https://github.com/vookimedlo/vooki-image-viewer"
_exiv2url="https://github.com/Exiv2/exiv2"
license=('GPL-3.0-only')
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}")
depends=(
    'expat'
    'qt6-base'
)
source=(
    "${pkgname%-bin}-${pkgver}.zip::${_ghurl}/releases/download/v${pkgver}/debian_stable_gcc_${pkgname%-bin}_${pkgver}_amd64.deb.zip"
    "exiv2-${_exiv2version}.zip::${_exiv2url}/releases/download/${_exiv2version}/exiv2-${_exiv2version}-Linux64.tar.gz"
    "${pkgname%-bin}.sh"
)
sha256sums=('8508398251c18fe9baa890c4fab503c324cd66e163a56f32fd1a146ec50d0bac'
            'caafad455c88f4d9cf8ab338204e8ffafac2110e7b8a332d296c274963abf460'
            '346cd32afa9661092ead7c6b2fbc954ae402343306cd0b8e74f5d723e3dc5f20')
build() {
    sed -e "s|@appname@|${pkgname%-bin}|g" \
        -e "s|@runname@|${_pkgname}|g" \
        -i "${srcdir}/${pkgname%-bin}.sh"
    bsdtar -xf "${srcdir}/${pkgname%-bin}_"*.deb
    bsdtar -xf "${srcdir}/data."*
    sed "s|Exec=${_pkgname}|Exec=${pkgname%-bin}|g;s|${pkgname%-bin}icon|${pkgname%-bin}|g" -i "${srcdir}/usr/share/applications/${pkgname%-bin}.desktop"
}
package() {
    install -Dm755 "${srcdir}/${pkgname%-bin}.sh" "${pkgdir}/usr/bin/${pkgname%-bin}"
    install -Dm755 "${srcdir}/usr/bin/${_pkgname}" -t "${pkgdir}/usr/lib/${pkgname%-bin}"
    install -Dm644 "${srcdir}/usr/lib/${pkgname%-bin}/imageformats/"*.so -t "${pkgdir}/usr/lib/${pkgname%-bin}/lib"
    install -Dm644 "${srcdir}/exiv2-${_exiv2version}-Linux64/lib/libexiv2.so"* -t "${pkgdir}/usr/lib/${pkgname%-bin}/lib"
    install -Dm644 "${srcdir}/usr/share/pixmaps/${pkgname%-bin}icon.png" "${pkgdir}/usr/share/pixmaps/${pkgname%-bin}.png"
    install -Dm644 "${srcdir}/usr/share/applications/${pkgname%-bin}.desktop" -t "${pkgdir}/usr/share/applications"
}